apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.mypopsy.floatingsearchview.demo"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "PROJECT_URL", '"https://github.com/renaudcerrato/FloatingSearchView"'
    }
    buildTypes {
        debug {
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext {
    supportLibVersion = '23.4.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':library')
    // Support
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    // ToggleDrawable
    compile 'com.github.renaudcerrato:ToggleDrawable:1.0.2'
    // Retrofit
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2'
    compile 'com.squareup.okhttp:logging-interceptor:2.7.0'
    // RxJava
    compile 'io.reactivex:rxandroid:1.2.1'
    // Dagger
    compile 'com.google.dagger:dagger:2.0.2'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    provided 'org.glassfish:javax.annotation:10.0-b28'
}

afterEvaluate { project ->
    android.applicationVariants.each { variant ->

        Task copyApk = task(("release" + variant.variantData.name.capitalize()), type:Copy) {
            from variant.outputs[0].outputFile
            destinationDir project.projectDir
            dependsOn variant.assemble
        }

        variant.assemble.finalizedBy(copyApk)
    }
}
